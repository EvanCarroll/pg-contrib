#!/usr/bin/perl -w

# Create dictionaries and affix rules palatable for PostgreSQL, using installed
# myspell and hunspell dictionaries.
#
# (C) 2008-2009 Martin Pitt <mpitt@debian.org>
# (C) 2012-2017 Christoph Berg <myon@debian.org>
#          2017 Evan Carroll <me@evancarroll.com>
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

use strict;
use feature ':5.24';
use autodie qw(:all);

# XXX - Remove this
use lib 'lib';

use DistroConfig::PostgreSQL::Dicts qw(
	install_to_pg_share
	copy_to_utf8_cache
	download_stopwords_to_utf8_cache
	scan_ispell
	cleanup_affix_dict
);

if ( $< != 0 ) {
	die "[$0] This script must be run as root\n"; 
}

say "Building PostgreSQL dictionaries from installed myspell/hunspell packages...";
my @to_install;
foreach my $locale ( @{scan_ispell()} ) {
	say "\t".$locale->{locale};
	say "\t\tconverting to utf8";

	my $affixdict = copy_to_utf8_cache($locale);
	say "\t\tdownloading stopwords";

	## Merge affix dict and {stop=>file}
	my $install = {
		locale => $affixdict->{locale},
		affix  => $affixdict->{affix},	
		dict   => $affixdict->{dict},	
		stop   => download_stopwords_to_utf8_cache($locale)
	};
	push @to_install, $install;
}

say "Installing PostgreSQL dictionaries:";
my %files;
foreach my $locale ( @to_install ) {
	$files{$_}=1 for @{install_to_pg_share($locale)};
}

cleanup_affix_dict \%files;


__END__

=head1 NAME

pg_updatedicts - build PostgreSQL dictionaries from myspell/hunspell ones

=head1 SYNOPSIS

B<pg_updatedicts>

=head1 DESCRIPTION

B<pg_updatedicts> makes dictionaries and affix files from installed myspell
and hunspell dictionary packages available to PostgreSQL for usage with tsearch
and word stem support. In particular, it takes all I<*.dic> and I<*.aff> files
from /usr/share/myspell/dicts/, converts them to UTF-8, puts them into
/var/cache/postgresql/dicts/ with I<*.dict> and I<*.affix> suffixes, and
symlinks them into /usr/share/postgresql/I<version>/tsearch_data/, where
PostgreSQL looks for them.

Through postgresql-common's dpkg trigger, this program is automatically run
whenever a myspell or hunspell dictionary package is installed or upgraded.

=head1 AUTHOR

Martin Pitt L<E<lt>mpitt@debian.orgE<gt>>
